// 2025-09-04 12:44:53
// Feather ignore all

function __PollenBufferReadGML(_buffer, _offset, _size)
{
    var _oldOffset = buffer_tell(_buffer);
    buffer_seek(_buffer, buffer_seek_start, _offset);
    var _parser = new ____PollenBufferReadGMLParser(_buffer, _size);
    buffer_seek(_buffer, buffer_seek_start, _oldOffset);
    return _parser.root;
}

function ____PollenBufferReadGMLParser(_buffer, _buffer_size) constructor
{
    root = {};
    
    buffer = _buffer;
    buffer_size = _buffer_size;
    depth = 0;
    line = 1;
    
    static read_root = function()
    {
        var _state = 0;
        var _key   = undefined;
        
        while(buffer_tell(buffer) < buffer_size)
        {
            token = undefined;
            token_is_real   = false;
            token_is_string = false;
            token_is_symbol = false;
            
            read_token();
            
            if (is_string(token))
            {
                if (!token_is_string && !token_is_symbol)
                {
                    try
                    {
                        token = real(token);
                        token_is_real = true;
                    }
                    catch(_)
                    {
                        token_is_real = false;
                    }
                }
                
                if (_state == 0)
                {
                    if (!token_is_real && !token_is_string && !token_is_symbol)
                    {
                        _key = token;
                        _state = 1;
                    }
                    else if (token_is_symbol && (token == ";"))
                    {
                        //Expected ; terminator
                    }
                    else
                    {
                        show_error("SNAP:\n\nLine " + string(line) + ", unexpected token " + string(token) + "\nis_string = " + string(token_is_string) + "\nis_real = " + string(token_is_real) + "\nis_symbol = " + string(token_is_symbol) + "\n ", true);
                        
                    }
                }
                else if (_state == 1)
                {
                    if (token_is_symbol && (token == "="))
                    {
                        _state = 2;
                    }
                    else
                    {
                        show_error("SNAP:\n\nLine " + string(line) + ", unexpected token " + string(token) + "\nis_string = " + string(token_is_string) + "\nis_real = " + string(token_is_real) + "\nis_symbol = " + string(token_is_symbol) + "\n ", true);
                    }
                }
                else if (_state == 2)
                {
                    if (token_is_symbol)
                    {
                        if (token == "{")
                        {
                            root[$ _key] = read_struct();
                            _state = 0;
                        }
                        else if (token == "[")
                        {
                            root[$ _key] = read_array();
                            _state = 0;
                        }
                        else
                        {
                            show_error("SNAP:\n\nLine " + string(line) + ", unexpected token " + string(token) + "\nis_string = " + string(token_is_string) + "\nis_real = " + string(token_is_real) + "\nis_symbol = " + string(token_is_symbol) + "\n ", true);
                        }
                    }
                    else
                    {
                        if (token_is_string)
                        {
                            token = string_replace_all(token, "\\\"", "\"");
                            token = string_replace_all(token, "\\\r", "\r");
                            token = string_replace_all(token, "\\\n", "\n");
                            token = string_replace_all(token, "\\\t", "\t");
                            token = string_replace_all(token, "\\\\", "\\");
                        }
                        else if (!token_is_real)
                        {
                            if (token == "false")
                            {
                                token = false;
                            }
                            else if (token == "true")
                            {
                                token = true;
                            }
                            else if (token == "undefined")
                            {
                                token = undefined;
                            }
                            else
                            {
                                var _asset_index = try_to_find_asset_index(token);
                                if (_asset_index >= 0)
                                {
                                    token = _asset_index;
                                }
                                else
                                {
                                    show_error("SNAP:\n\nLine " + string(line) + ", unexpected token " + string(token) + "\nis_string = " + string(token_is_string) + "\nis_real = " + string(token_is_real) + "\nis_symbol = " + string(token_is_symbol) + "\n ", true);
                                }
                            }
                        }
                        
                        root[$ _key] = token;
                        _state = 0;
                    }
                }
            }
        }
    }
    
    static read_array = function()
    {
        var _array = [];
        var _state = 0;
        
        while(buffer_tell(buffer) < buffer_size)
        {
            token = undefined;
            token_is_real   = false;
            token_is_string = false;
            token_is_symbol = false;
            
            read_token();
            
            if (is_string(token))
            {
                if (!token_is_string && !token_is_symbol)
                {
                    try
                    {
                        token = real(token);
                        token_is_real = true;
                    }
                    catch(_)
                    {
                        token_is_real = false;
                    }
                }
                
                if (_state == 0)
                {
                    if (token_is_symbol)
                    {
                        if (token == "{")
                        {
                            array_push(_array, read_struct());
                            _state = 1;
                        }
                        else if (token == "[")
                        {
                            array_push(_array, read_array());
                            _state = 1;
                        }
                        else if (token == "]")
                        {
                            break;
                        }
                        else
                        {
                            show_error("SNAP:\n\nLine " + string(line) + ", unexpected token " + string(token) + "\nis_string = " + string(token_is_string) + "\nis_real = " + string(token_is_real) + "\nis_symbol = " + string(token_is_symbol) + "\n ", true);
                        }
                    }
                    else
                    {
                        if (token_is_string)
                        {
                            token = string_replace_all(token, "\\\"", "\"");
                            token = string_replace_all(token, "\\\r", "\r");
                            token = string_replace_all(token, "\\\n", "\n");
                            token = string_replace_all(token, "\\\t", "\t");
                            token = string_replace_all(token, "\\\\", "\\");
                        }
                        else if (!token_is_real)
                        {
                            if (token == "false")
                            {
                                token = false;
                            }
                            else if (token == "true")
                            {
                                token = true;
                            }
                            else if (token == "undefined")
                            {
                                token = undefined;
                            }
                            else if (string_starts_with(token, "#") && string_length(token) == 7)
                            {
                                var _hexValue = hex_string_to_number(token);
                                if(_hexValue == undefined){show_error("SNAP:\n\nLine " + string(line) + ", invalid hex value " + string(token) + "\n ", true);}
                                token = _hexValue;
                            }
                            else
                            {
                                var _asset_index = try_to_find_asset_index(token);
                                if (_asset_index >= 0)
                                {
                                    token = _asset_index;
                                }
                                else
                                {
                                    show_error("SNAP:\n\nLine " + string(line) + ", unexpected token " + string(token) + "\nis_string = " + string(token_is_string) + "\nis_real = " + string(token_is_real) + "\nis_symbol = " + string(token_is_symbol) + "\n ", true);
                                }
                            }
                        }
                        
                        array_push(_array, token);
                        _state = 1;
                    }
                }
                else if (_state == 1)
                {
                    if (token_is_symbol && (token == "]"))
                    {
                        break;
                    }
                    else if (token_is_symbol && (token == ","))
                    {
                        _state = 0;
                    }
                    else
                    {
                        show_error("SNAP:\n\nLine " + string(line) + ", unexpected token " + string(token) + "\nis_string = " + string(token_is_string) + "\nis_real = " + string(token_is_real) + "\nis_symbol = " + string(token_is_symbol) + "\n ", true);
                    }
                }
            }
        }
        
        return _array;
    }
    
    static read_struct = function()
    {
        var _struct = {};
        var _state  = 0;
        var _key    = undefined;
        
        while(buffer_tell(buffer) < buffer_size)
        {
            token = undefined;
            token_is_real   = false;
            token_is_string = false;
            token_is_symbol = false;
            
            read_token();
            
            if (is_string(token))
            {
                if (!token_is_string && !token_is_symbol)
                {
                    try
                    {
                        token = real(token);
                        token_is_real = true;
                    }
                    catch(_)
                    {
                        token_is_real = false;
                    }
                }
                
                if (_state == 0)
                {
                    if (!token_is_real && !token_is_string && !token_is_symbol)
                    {
                        _key = token;
                        _state = 1;
                    }
                    else if (token_is_symbol && (token == ","))
                    {
                        //Expected ; terminator
                    }
                    else if (token_is_symbol && (token == "}"))
                    {
                        break;
                    }
                    else
                    {
                        show_error("SNAP:\n\nLine " + string(line) + ", unexpected token " + string(token) + "\nis_string = " + string(token_is_string) + "\nis_real = " + string(token_is_real) + "\nis_symbol = " + string(token_is_symbol) + "\n ", true);
                    }
                }
                else if (_state == 1)
                {
                    if (token_is_symbol && (token == ":"))
                    {
                        _state = 2;
                    }
                    else
                    {
                        show_error("SNAP:\n\nLine " + string(line) + ", unexpected token " + string(token) + "\nis_string = " + string(token_is_string) + "\nis_real = " + string(token_is_real) + "\nis_symbol = " + string(token_is_symbol) + "\n ", true);
                    }
                }
                else if (_state == 2)
                {
                    if (token_is_symbol)
                    {
                        if (token == "{")
                        {
                            _struct[$ _key] = read_struct();
                            _state = 0;
                        }
                        else if (token == "[")
                        {
                            _struct[$ _key] = read_array();
                            _state = 0;
                        }
                        else if (token_is_symbol && (token == "}"))
                        {
                            break;
                        }
                        else
                        {
                            show_error("SNAP:\n\nLine " + string(line) + ", unexpected token " + string(token) + "\nis_string = " + string(token_is_string) + "\nis_real = " + string(token_is_real) + "\nis_symbol = " + string(token_is_symbol) + "\n ", true);
                        }
                    }
                    else
                    {
                        if (token_is_string)
                        {
                            token = string_replace_all(token, "\\\"", "\"");
                            token = string_replace_all(token, "\\\r", "\r");
                            token = string_replace_all(token, "\\\n", "\n");
                            token = string_replace_all(token, "\\\t", "\t");
                            token = string_replace_all(token, "\\\\", "\\");
                        }
                        else if (!token_is_real)
                        {
                            if (token == "false")
                            {
                                token = false;
                            }
                            else if (token == "true")
                            {
                                token = true;
                            }
                            else if (token == "undefined")
                            {
                                token = undefined;
                            }
                            else if (string_starts_with(token, "#") && string_length(token) == 7)
                            {
                                var _hexValue = hex_string_to_number(token);
                                if(_hexValue == undefined){show_error("SNAP:\n\nLine " + string(line) + ", invalid hex value " + string(token) + "\n ", true);}
                                token = _hexValue;
                            }
                            else
                            {
                                var _asset_index = try_to_find_asset_index(token);
                                if (_asset_index >= 0)
                                {
                                    token = _asset_index;
                                }
                                else
                                {
                                    show_error("SNAP:\n\nLine " + string(line) + ", unexpected token " + string(token) + "\nis_string = " + string(token_is_string) + "\nis_real = " + string(token_is_real) + "\nis_symbol = " + string(token_is_symbol) + "\n ", true);
                                }
                            }
                        }
                        
                        _struct[$ _key] = token;
                        _state = 0;
                    }
                }
            }
        }
        
        return _struct;
    }
    
    static read_token = function()
    {
        var _token_start      = undefined;
        var _in_string        = false;
        var _in_line_comment  = false;
        var _in_block_comment = false;
        
        while(buffer_tell(buffer) < buffer_size)
        {
            var _value = buffer_read(buffer, buffer_u8);
            
            if (_value == 10) line++;
            
            if (_in_string)
            {
                if ((_value == 34) && (buffer_peek(buffer, buffer_tell(buffer) - 2, buffer_u8) != 92))
                {
                    buffer_poke(buffer, buffer_tell(buffer) - 1, buffer_u8, 0x0);
                    buffer_seek(buffer, buffer_seek_start, _token_start + 1); //Skip the leading double quote
                    token = buffer_read(buffer, buffer_string);
                    buffer_poke(buffer, buffer_tell(buffer) - 1, buffer_u8, _value);
                    
                    token_is_string = true;
                    token_is_symbol = false;
                    break;
                }
            }
            else if (_in_line_comment)
            {
                if (_value == 10) _in_line_comment = false;
            }
            else if (_in_block_comment)
            {
                if ((_value == 47) && (buffer_peek(buffer, buffer_tell(buffer) - 2, buffer_u8) == 42))
                {
                    _in_block_comment = false;
                }
            }
            else
            {
                if (_token_start == undefined)
                {
                    if (_value > 32)
                    {
                        if ((_value == 47) && (buffer_peek(buffer, buffer_tell(buffer), buffer_u8) == 47))
                        {
                            //Line comment
                            _in_line_comment = true;
                        }
                        else if ((_value == 47) && (buffer_peek(buffer, buffer_tell(buffer), buffer_u8) == 42))
                        {
                            //Block comment
                            _in_block_comment = true;
                        }
                        else
                        {
                            _token_start = buffer_tell(buffer) - 1;
                            if (_value == 34) _in_string = true;
                            
                            if ((_value ==  44)  // ,
                            ||  (_value ==  58)  // :
                            ||  (_value ==  59)  // ;
                            ||  (_value ==  61)  // =
                            ||  (_value ==  91)  // [
                            ||  (_value ==  93)  // ]
                            ||  (_value == 123)  // {
                            ||  (_value == 125)) // }
                            {
                                token = chr(_value);
                                token_is_string = false;
                                token_is_symbol = true;
                                break;
                            }
                        }
                    }
                }
                else if ((_value <=  32)  // whitespace
                     ||  (_value ==  44)  // ,
                     ||  (_value ==  58)  // :
                     ||  (_value ==  59)  // ;
                     ||  (_value ==  61)  // =
                     ||  (_value ==  91)  // [
                     ||  (_value ==  93)  // ]
                     ||  (_value == 123)  // {
                     ||  (_value == 125)) // }
                {
                    buffer_poke(buffer, buffer_tell(buffer) - 1, buffer_u8, 0x0);
                    buffer_seek(buffer, buffer_seek_start, _token_start);
                    token = buffer_read(buffer, buffer_string);
                    buffer_seek(buffer, buffer_seek_relative, -1);
                    buffer_poke(buffer, buffer_tell(buffer), buffer_u8, _value);
                    
                    token_is_string = false;
                    token_is_symbol = false;
                    break;
                }
            }
        }
    }
    
    static try_to_find_asset_index = function(_asset)
    {
        static _constantStruct = {
            noone: noone,
            all: all,
            
            //Colours
            c_black:   c_black,
            c_blue:    c_blue,
            c_dkgray:  c_dkgray,
            c_dkgrey:  c_dkgrey,
            c_fuchsia: c_fuchsia,
            c_gray:    c_gray,
            c_grey:    c_grey,
            c_green:   c_green,
            c_lime:    c_lime,
            c_ltgray:  c_ltgray,
            c_ltgrey:  c_ltgrey,
            c_maroon:  c_maroon,
            c_navy:    c_navy,
            c_olive:   c_olive,
            c_purple:  c_purple,
            c_red:     c_red,
            c_silver:  c_silver,
            c_teal:    c_teal,
            c_white:   c_white,
            c_yellow:  c_yellow,
            c_orange:  c_orange,
            
            //Part type shapes
            pt_shape_pixel: pt_shape_pixel,
            pt_shape_disk: pt_shape_disk,
            pt_shape_square: pt_shape_square,
            pt_shape_line: pt_shape_line,
            pt_shape_star: pt_shape_star,
            pt_shape_circle: pt_shape_circle,
            pt_shape_ring: pt_shape_ring,
            pt_shape_sphere: pt_shape_sphere,
            pt_shape_flare: pt_shape_flare,
            pt_shape_spark: pt_shape_spark,
            pt_shape_explosion: pt_shape_explosion,
            pt_shape_cloud: pt_shape_cloud,
            pt_shape_smoke: pt_shape_smoke,
            pt_shape_snow: pt_shape_snow,
    
        };
        
        if (!is_string(_asset)) return _asset;
        
        var _index = asset_get_index(_asset);
        if (_index >= 0) return _index;
        
        if (!variable_struct_exists(_constantStruct, _asset)) return -1;
        return _constantStruct[$ _asset];
    }
    
    static hex_string_to_number = function(hex_string) 
    {
        var _hex_string = string_lower(string_lettersdigits(hex_string));
        var _value = 0;
        var _len = string_length(_hex_string); 
        for (var i = 1; i <= _len; i++) 
        {
            var _char = string_char_at(_hex_string, i);
            var _digit_val = 0;
            switch (_char) 
            {
                case "0": _digit_val = 0; break;
                case "1": _digit_val = 1; break;
                case "2": _digit_val = 2; break;
                case "3": _digit_val = 3; break;
                case "4": _digit_val = 4; break;
                case "5": _digit_val = 5; break;
                case "6": _digit_val = 6; break;
                case "7": _digit_val = 7; break;
                case "8": _digit_val = 8; break;
                case "9": _digit_val = 9; break;
                case "a": _digit_val = 10; break;
                case "b": _digit_val = 11; break;
                case "c": _digit_val = 12; break;
                case "d": _digit_val = 13; break;
                case "e": _digit_val = 14; break;
                case "f": _digit_val = 15; break;
                default: return undefined; // Invalid hex character
            } 
            _value = _value * 16 + _digit_val;
        }
        return bgr_to_rgb(_value);
    }

    static bgr_to_rgb = function(_c) {
        return ((_c & 0xFF) << 16) | (_c & 0xFF00) | ((_c >> 16) & 0xFF);
    }
    
    read_root();
}